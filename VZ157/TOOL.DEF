======== tool.def (v1.5) ========

* M マクロ

70 [ESC]H "【ツールメニュー】"
	!01

71 [ESC]Y "指定桁折"
	?. (yy)?? yy=70,
	&g("桁折位置($yy)")
	(r==-1)?.
	(r>0)? yy=r,
	tt="‘“（〔［｛〈《「『【([{｢",
	ee="’”）〕］｝〉》」』】、。，．？！!),.?]}｣",
	mi[, ei[, mi-, ei+,
:A	&k (r==$1B)? >Z			; [ESC]が押されたら中断
	&x(yy)
	(ct)?? >Z
	(ct==1)? { #x >A }
:B	#s &f(cd,tt) (r>=0)? >B
:C	#d &f(cd,ee) (r>=0)? >C
	#m >A
:Z	ei], mi],

72 [ESC]B "ブロックのソート"
	?. (mb)??.
	&s
	#S "temp.src" #m (s)? 'Y'
	#E "sort <temp.src >temp.dst" #m #[
	#y
	#R "temp.dst" #m
	#b #_ #y #C #i
	#E "del temp.*" #m
	&d #[

73 [ESC]H "センタリング"
	?. (mb)?.
	&s
	#< (ct<2)? >Z
	(ct==2)? #t
	#> x=(we-lx)/2,
	#< &#68(x)
:Z	#z
	&d

74 [ESC]K "全角スペースの表示"
	dz[, dz=$A000,
	p=0,
:A	(p..0==$2101)?{
		p..0=$2202, p..1=$A202,
		p[,
		p+=$2000,
		p..0=$C1, p..1=$C1,
		p],
	}
	((p+=4)<$1000)? >A
	dz],

;【文字トレースマクロ】
;　入力した文字をカーソルキーで上下左右へコピー
;　[CR]で一時的にトレースをＯＦＦ
;　[ESC]で終了

75 [ESC]T "文字トレース"
	?.
	&m("文字トレース（[ESC]で終了）")
	mi[, ei[, eu[, mi+, ei+, eu+, ep+,
:M	c=0,
:A	&p  d=r, (r<$100)?{ c=r, >B }
	(r==#[)? >E
	(c)?{ (r-$104)>?{ >M >U >D >L >R } } &o(d) >A
:R	#d >B
:L	#s (cd>0 && cd<$20)? >A >B
:D	x=kx, #x (r)?{ #> #m } >V
:U	x=kx, #e (r)? >A
:V	(ct<=1)?{ #> &#68(x) #> }
:B	(c<0 && cd>$1F)?{ (d==#s)? #h ? #g }
	&o(c) #s >A
:E
	ep-, eu], ei], mi],
	&m("")

* P メニュー

1	"",34,5,20
	&71 &72 &73 &74 &75

*
======== End of tool.def ========
