======== block.def (v1.57) ========
;
;【組み込み方法】
;
;・ A>vz +block ...
;・ vz.def: * E その他 1 block
;
;
;【矩形ブロックの操作方法】
;
;・ ^KB :矩形ブロックモード
;・ ^Y,^KK,^J :矩形ブロックの削除／記憶／挿入
;
;※　矩形ブロックの複写（^KC）はできません。
;※　複数行のまたがる行内では正常に動作しません。

* M マクロ

59 [ESC]K "【矩形ブロック】 [ESC]K"
	!01

60 ^KB "矩形ブロックモード"
	?. #b (mb)?{ mb=4, xb=lx, }

61 ^Y [F08] "矩形ブロック削除"
	m-, >01

62 ^KK \[F08] "矩形ブロック記憶"
	m+, >01
1:
	?. (mb != 4)?{ cc=0, (m)? #k ? #y . }
	&s
	xx=-1, (lx > xb)? xx=lx,
	n=ln, #b #B (ln < n)?{ n=ln, #B }
	cc=0,
:A	(ln <= n)? >Z #e
	&x(xb) #b
	(xx < 0)? #> ? &x(xx)
	(m)? #k ? #y
	cc++, >A
:Z	&d	

63 ^J [F09] "矩形ブロック挿入"
	?. (cc)??{ #i . }
	xx=lx,
	ei[, ek[, ei-, ek-,
	&s
:A	(ct <= 1)?{ #> &#68(xx) #> }
	#i #x (r)?{ #> #m }
	(cc--)? >A
	ek], ei],
	&d

65 [ESC]] "行末のSPACE･TABの削除"
	?. (mb)??.
	&s
	n=ln, #b #B (ln > n)?{ n=ln, #B }
:A	(ln >= n)? >Z
	#>
	(lx)?{
		#s (ct == 2)?{ #d #16 }
	}
	#x >A
:Z	&d

* P メニュー

1	"",34,3,22
	&61 &65
	"TAB→SPACE変換  ",Eu

*
======== End of block.def ========
